<!DOCTYPE html>
<html>
    <head>
        <title></title>
        <script id="shader-vertex" type="x-shader/x-vertex">
            //position of a vertex.
            attribute vec4 aVertexPosition; 
            //vertex normal.
            attribute vec3 aNormal; 

            //mvp matrix
            uniform mat4 uMVPMatrix; 
            //normal matrix
            uniform mat3 uNormalMatrix; 

            //The direction of light. This is used for diffuse light calculation. 
            uniform vec3 uLightDir; 

            //shadow mvp matrix
            uniform mat4 uShadowMVPMatrix;

            //for diffuse reflection calculation.
            varying float v_Dot; 

            //for shadow calculation
            varying vec4 v_PositionFromLight;

            void main() {
                //matrix 的 cross product 不是 commutative 的，所以这里两个变量的先后顺序不能调换。
                //(vector aVertexPosition 可以被视为一维的 matrix)          
                gl_Position = uMVPMatrix * aVertexPosition;

                /*
                 * 下面两句的定义详见day01日记
                 */
                vec3 normal = uNormalMatrix * aNormal;
                v_Dot = max(dot(uLightDir, normalize(normal)), 0.0);

                v_PositionFromLight = uShadowMVPMatrix * aVertexPosition;
            }
        </script>

        <script id="shader-fragment" type="x-shader/x-fragment">
            precision mediump float;

            uniform vec3 uAmbientLightColor;
            uniform vec4 uColor; //object color

            uniform sampler2D uShadowMap;

            varying float v_Dot;
            varying vec4 v_PositionFromLight;

            float unpackDepth(const in vec4 rgbaDepth) {
                const vec4 bitShift = vec4(1.0, 1.0/256.0, 1.0/(256.0*256.0), 1.0/(256.0*256.0*256.0));
                float depth = dot(rgbaDepth, bitShift);
                return depth;
            }

            void main() {
                vec3 ambientFactor = uAmbientLightColor * uColor.rgb;
                vec3 shadowCoord = (v_PositionFromLight.xyz/v_PositionFromLight.w)/2.0 + 0.5;
                vec4 rgbaDepth = texture2D(uShadowMap, shadowCoord.xy);
                float depth = unpackDepth(rgbaDepth);
                float visibility = (shadowCoord.z > depth + 0.0015) ? 0.0 : 1.0;

                //colorWithDiffuseLight的计算请参加日记 (light color * base color * dot product)
                vec4 colorWithDiffuseLight = vec4(vec3(1.0, 1.0, 1.0) * uColor.xyz * v_Dot, uColor.a);
                vec4 colorWithDiffuseLightAndShadow = vec4(colorWithDiffuseLight.rgb * visibility, colorWithDiffuseLight.a);
                gl_FragColor = vec4(colorWithDiffuseLightAndShadow.rgb + ambientFactor, colorWithDiffuseLightAndShadow.a);
            }
        </script>

        <script id="shadow-shader-vertex" type="x-shader/x-vertex">
            attribute vec4 aVertexPosition;
            uniform mat4 uMVPMatrix;

            void main() {
                gl_Position = uMVPMatrix * aVertexPosition;
            }
        </script>

        <script id="shadow-shader-fragment" type="x-shader/x-vertex">
            precision mediump float;

            void main() {
                const vec4 bitShift = vec4(1.0, 256.0, 256.0 * 256.0, 256.0 * 256.0 * 256.0);
                const vec4 bitMask = vec4(1.0/256.0, 1.0/256.0, 1.0/256.0, 0.0);
                vec4 rgbaDepth = fract(gl_FragCoord.z * bitShift);
                rgbaDepth -= rgbaDepth.gbaa * bitMask;

                gl_FragColor = rgbaDepth;
            }
        </script>

        <meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
    </head>
    <body>
        <canvas id="canvas" width="1200" height="800"></canvas>
        <script type="text/javascript" src="../gl-matrix.js"></script>
        <script type="text/javascript" src="main.js"></script>
    </body>
</html>